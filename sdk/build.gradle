apply plugin: 'jacoco'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'

def currentVersionName = "3.0.3"
def getVersionName = { ->
    return currentVersionName
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        project.archivesBaseName = "sdk"
        project.version = currentVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }
    buildToolsVersion = '29.0.1'

}
project(":sdk") {
    sonarqube {
        properties {
            property "sonar.projectKey", "drive-sdk-android-next"
            property "sonar.sources", "src/main"
            property "sonar.tests", "src/test"
            property "sonar.host.url", "https://cicd.next.dil.services/sonarqube"
            property "sonar.coverage.jacoco.xmlReportPaths", "build/coverageReport/testDebugUnitTestCoverage/testDebugUnitTestCoverage.xml"
            property "sonar.dependencyCheck.reportPath", "dependency-check-report.xml"
            property "sonar.dependencyCheck.htmlReportPath", "dependency-check-report.html"
        }
    }
}
project.tasks["sonarqube"].dependsOn "testDebugUnitTestCoverage"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Google Location service
    def play_services_version = "17.0.0"
    implementation "com.google.android.gms:play-services-location:$play_services_version"

    // Koin
    def koin_version = '2.0.1'
    implementation "org.koin:koin-android:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation "org.koin:koin-test:$koin_version"


    // WorkManager
    def work_version = "2.2.0"
    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$work_version"

    //Rx
    def rx_kotlin_version = '2.4.0'
    implementation("io.reactivex.rxjava2:rxkotlin:$rx_kotlin_version")

    // Jackson
    def jackson_version = '2.10.0'
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"


    // JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

}
repositories {
    mavenCentral()
    jcenter()
}


def libraryGroupId = 'axa.tex.drive'
def libraryArtifactId = "${archivesBaseName}".toLowerCase()

publishing {
    publications {
        android.libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def buildType = output.name // output.name is {debug, release}
                "${buildType}"(MavenPublication) {
                    //groupId libraryGroupId
                    version currentVersionName
                    //artifactId "${libraryArtifactId}-sdk-${buildType}" // texdrive-sdk-{release,debug}
                    artifact("$buildDir/outputs/aar/sdk-${buildType}.aar") // TexDrive-{release,debug}-v2.1.3.aar


                    pom.withXml {
                        def dependencies = asNode().appendNode('dependencies')
                        configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                            def dependency = dependencies.appendNode('dependency')
                            dependency.appendNode('groupId', it.moduleGroup)
                            dependency.appendNode('artifactId', it.moduleName)
                            dependency.appendNode('version', it.moduleVersion)
                        }
                    }

                }
            }
        }
    }
}


artifactory {
    contextUrl = 'https://axadil.jfrog.io/axadil/'
    publish {
        repository {
            repoKey = 'android-tex'
            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('release')
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

jacoco {
    toolVersion = "0.8.5"
    // Custom reports directory can be specfied like this:
    reportsDir = file("$buildDir/coverageReport")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

project.afterEvaluate {
    android.libraryVariants.all { variant ->
        def variantName = variant.name
        def testTaskName = "test${variantName.capitalize()}UnitTest"

        tasks.create(name: "${testTaskName}Coverage", type: JacocoReport, dependsOn: "$testTaskName") {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${variantName.capitalize()} build."

            reports {
                html.enabled = true
                xml.enabled = true
            }

            def excludes = [
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    'android/**/*.*'
            ]
            def javaCompileTask
            if (variant.hasProperty('javaCompileProvider')) {
                // Android 3.3.0+
                javaCompileTask = variant.javaCompileProvider.get()
            } else {
                javaCompileTask = variant.javaCompile
            }
            def javaClasses = fileTree(dir: javaCompileTask.destinationDir, excludes: excludes)
            def kotlinClasses = fileTree(dir: "${buildDir}/tmp/kotlin-classes/${variantName}", excludes: excludes)
            classDirectories = files([javaClasses, kotlinClasses])

            sourceDirectories = files([
                    "$project.projectDir/src/main/java",
                    "$project.projectDir/src/${variantName}/java",
                    "$project.projectDir/src/main/kotlin",
                    "$project.projectDir/src/${variantName}/kotlin"
            ])

            executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")
        }
    }
}
